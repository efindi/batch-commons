plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "io.codearte.nexus-staging" version "0.21.2"
    id "idea"
}

ext.springBatchVersion = "4.2.2.RELEASE"
ext.immutableVersion = "2.8.2"
ext.junitVersion = "5.4.1"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group "com.efindi"
version "0.0.3-SNAPSHOT"

repositories {
    mavenCentral()
}

dependencies {
    implementation "org.springframework.batch:spring-batch-core:${springBatchVersion}"
    implementation "org.immutables:value:${immutableVersion}"
    annotationProcessor "org.immutables:value:${immutableVersion}"

    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "org.mockito:mockito-junit-jupiter:2.25.0"
}

jar {
    def vendor = "efindi"
    String[] packageArr = ["com", "efindi", "batch", "commons"]
    from("LICENSE-2.0.txt") {
        into("META-INF")
    }
    manifest {
        attributes(
                "Name": String.join("/", packageArr).concat("/"),
                "Specification-Title": "Spring Batch Commons",
                "Specification-Version": "1",
                "Specification-Vendor": vendor,
                "Implementation-Title": String.join(".", packageArr),
                "Implementation-Version": version,
                "Implementation-Vendor": vendor,
                "Created-By": System.getProperty("java.version") + " (" + System.getProperty("java.vendor") + ")",
                "Built-With": "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}"
        )
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}

test {
    useJUnitPlatform()
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = "sources"
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = "javadoc"
}

artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    publications {
        def gitUrl = "https://github.com/efindi/batch-commons"
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name = Project.getName()
                description = "Efindi Batch Commons"
                url = gitUrl
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                developers {
                    developer {
                        id = getDeveloperProperty("id")
                        name = getDeveloperProperty("name")
                        email = getDeveloperProperty("email")
                    }
                }
                scm {
                    connection = "scm:git:git@github.com:efindi/batch-commons.git"
                    developerConnection = "scm:git:ssh://git@github.com:efindi/batch-commons.git"
                    url = gitUrl
                }
            }
        }
    }
    repositories {
        maven {
            // change URLs to point to your repos, e.g. http://my.org/repo
            def releasesRepoUrl = "$buildDir/repos/releases"
            def snapshotsRepoUrl = "$buildDir/repos/snapshots"
//            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
//            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith("SNAPSHOT") ? snapshotsRepoUrl : releasesRepoUrl
//            credentials(PasswordCredentials) {
//                username = getOssrhProperty("username")
//                password = getOssrhProperty("password")
//            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

def getOssrhProperty(String name) {
    name = String.join(".", "ossrh", name)
    if (project.hasProperty(name)) {
        return project.getProperties()[name]
    } else {
        return ""
    }
}

def getDeveloperProperty(String name) {
    name = String.join(".", "developer", name)
    if (project.hasProperty(name)) {
        return project.getProperties()[name]
    } else {
        return ""
    }
}

nexusStaging {
    username = getOssrhProperty("username")
    password = getOssrhProperty("password")
}
